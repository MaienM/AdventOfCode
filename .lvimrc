function! s:GetFileInfo()
	let l:fn = expand("%")
	let l:parts = matchlist(l:fn, "\\([^/]\\+\\)/src/\\([^/]\\+\\)/\\(.*\\)\\.rs")
	if l:parts == []
		echo "Action not available for this file."
		return v:false
	end
	let s:crate = l:parts[1]
	let s:type = l:parts[2]
	let s:name = l:parts[3]
	if s:type == "bin"
		if s:crate == s:name
			let s:scope = "series"
			let s:target = s:crate
		else
			let s:scope = "chapter"
			let s:target = s:crate . "-" . s:name
		end
	end
	return v:true
endfunction

function! s:TestRun()
	if !s:GetFileInfo()
		return
	end
	if s:type == "bin"
		call VimuxRunCommand("clear; make test-and-run-" . s:target)
	else
		call VimuxRunCommand("clear; make test-libs")
	endif
endfunction

function! s:Bench(baseline)
	if !s:GetFileInfo()
		return
	end
	if s:type == "bin" && s:scope == "chapter"
		call VimuxRunCommand("clear; make benchmark-" . (a:baseline ? "set-baseline-" : "") . s:target)
	else
		echo "No benchmarks for this file."
	endif
endfunction

function! s:Profile(baseline)
	if !s:GetFileInfo()
		return
	end
	if s:type == "bin" && s:scope == "chapter"
		call VimuxRunCommand("clear; make profile-" . (a:baseline ? "set-baseline-" : "") . s:target)
	else
		echo "No benchmarks for this file."
	endif
endfunction

function! s:RunAll()
	call VimuxRunCommand("clear; make run-all")
endfunction

function! s:FindInput()
	if !s:GetFileInfo()
		return v:false
	end
	if s:type == "bin" && s:scope == "chapter"
		return "inputs/" . s:crate . "/" . s:name . "/input.txt"
	else
		echo "No input for this file."
		return v:false
	fi
endfunction

function! s:OpenInput()
	let l:input = s:FindInput()
	if l:input == v:false
		return
	end
	exe "e " . l:input
endfunction

function! s:ShowInput()
	let l:input = s:FindInput()
	if l:input == v:false
		return
	end
	call VimuxRunCommand("cat " . l:input)
endfunction

function! s:OpenWebsite()
	if !s:GetFileInfo()
		return
	end
	if s:crate != "aoc" || s:type != "bin" || s:scope != "chapter"
		echo "No URL available for this file."
		return
	end
	let l:parts = split(s:name, "-")
	let l:year = "20" . l:parts[0]
	let l:day = substitute(l:parts[1], "^0", "", "")
	call system("xdg-open https://adventofcode.com/" . l:year . "/day/" . l:day)
endfunction

augroup LVIMRC
	au!
	au BufEnter *.rs nnoremap <buffer> <leader>rt :call <SID>TestRun()<CR>
	au BufEnter *.rs nnoremap <buffer> <leader>rb :call <SID>Bench(v:false)<CR>
	au BufEnter *.rs nnoremap <buffer> <leader>rB :call <SID>Bench(v:true)<CR>
	au BufEnter *.rs nnoremap <buffer> <leader>rp :call <SID>Profile(v:false)<CR>
	au BufEnter *.rs nnoremap <buffer> <leader>rP :call <SID>Profile(v:true)<CR>
	au BufEnter *.rs nnoremap <buffer> <leader>rT :call <SID>RunAll()<CR>
	au BufEnter *.rs nnoremap <buffer> <leader>gi :call <SID>OpenInput()<CR>
	au BufEnter *.rs nnoremap <buffer> <leader>gI :call <SID>ShowInput()<CR>
	au BufEnter *.rs nnoremap <buffer> <leader>gb :call <SID>OpenWebsite()<CR>
augroup END
